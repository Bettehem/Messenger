apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    int appVersionCode = 10
    String appVersionName = "0.0.10"




    
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    signingConfigs {
        messengerKey {
            keyAlias properties.getProperty('keystore.keyAlias')
            keyPassword properties.getProperty('keystore.keyPassword')
            storeFile file(properties.getProperty('keystore.path'))
            storePassword properties.getProperty('keystore.password')
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    defaultConfig {
        applicationId "com.github.bettehem.messenger"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode appVersionCode
        versionName appVersionName
        multiDexEnabled true
        signingConfig signingConfigs.messengerKey
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }
    flavorDimensions "api", "mode"
    productFlavors {
        alpha {
            dimension "mode"
            applicationId 'com.github.bettehem.messenger'
            versionCode appVersionCode
            versionNameSuffix "-alpha"
        }
        beta {
            dimension "mode"
            applicationId 'com.github.bettehem.messenger'
            versionCode appVersionCode
            versionNameSuffix "-beta"
        }
        prod {
            dimension "mode"
            applicationId 'com.github.bettehem.messenger'
            versionCode appVersionCode
            versionName appVersionName
        }

        minApi14 {
            dimension "api"
            minSdkVersion "14"
            multiDexEnabled true
        }

        minApi23 {
            dimension "api"
            minSdkVersion "23"
        }
    }
    useLibrary 'org.apache.http.legacy'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.firebase:firebase-core:12.0.1'
    implementation 'com.google.firebase:firebase-auth:12.0.1'
    implementation 'com.google.firebase:firebase-database:12.0.1'
    implementation 'com.google.firebase:firebase-crash:12.0.1'
    implementation 'com.google.firebase:firebase-messaging:12.0.1'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.github.bettehem:androidtools:1.1.75'
    implementation 'com.rockerhieu.emojicon:library:1.3.3'
    implementation files('libs/commons-codec-1.7-SNAPSHOT-android.jar')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.api-client:google-api-client:1.23.0'
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
